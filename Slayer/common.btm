RULE 默认功能插件 显示功能 快捷键 LCTRL+UP
CLASS com.megacrit.cardcrawl.core.CardCrawlGame
METHOD updateDebugSwitch()
AT ENTRY
# LCTRL + UP
IF com.badlogic.gdx.Gdx.input.isKeyPressed(129)
   AND
   com.badlogic.gdx.Gdx.input.isKeyJustPressed(19)
DO traceln("called SimpleHelper.switchUI()");
   abc.def.SimpleHelper.switchUI()
ENDRULE

RULE 默认功能插件 设置奖励卡牌
CLASS com.megacrit.cardcrawl.dungeons.AbstractDungeon
METHOD getRewardCards()
AT EXIT
IF TRUE
DO 
abc.def.SimpleHelper.adjustCardReward($!);
ENDRULE

RULE 默认功能插件 设置奖励卡牌+遗物 加强
CLASS com.megacrit.cardcrawl.screens.CombatRewardScreen
METHOD setupItemReward()
AT WRITE hasTakenAll
IF TRUE
DO 
abc.def.SimpleHelper.adjustRewardForSelect($this.rewards);
ENDRULE

RULE Beta开关+总有开局奖励
CLASS com.megacrit.cardcrawl.core.CardCrawlGame
METHOD create()
AT ENTRY
IF TRUE
DO
#Beta开关 true:开启,false:关闭.默认开启 
com.megacrit.cardcrawl.core.Settings.isBeta=true;
#总有开局奖励
com.megacrit.cardcrawl.core.Settings.isTestingNeow=true;
ENDRULE

RULE 卡牌遗物全部解锁
CLASS com.megacrit.cardcrawl.core.CardCrawlGame
METHOD create()
AT EXIT
IF TRUE
DO 
#com.megacrit.cardcrawl.helpers.CardLibrary.unlockAndSeeAllCards();
#com.megacrit.cardcrawl.helpers.RelicLibrary.unlockAndSeeAllRelics();
ENDRULE

RULE 遗物 御守 加强 无限移除诅咒牌
CLASS com.megacrit.cardcrawl.relics.Omamori
METHOD setCounter(int)
AT ENTRY
IF true
DO 
$1=2;
ENDRULE

RULE 使用任意牌后随机牌消耗变 0 
CLASS com.megacrit.cardcrawl.actions.utility.UseCardAction
METHOD <init>(com.megacrit.cardcrawl.cards.AbstractCard,com.megacrit.cardcrawl.core.AbstractCreature)
AT ENTRY
BIND
actionManager=com.megacrit.cardcrawl.dungeons.AbstractDungeon.actionManager;
player=com.megacrit.cardcrawl.dungeons.AbstractDungeon.player;
IF true
DO 
#actionManager.addToBottom(new com.megacrit.cardcrawl.actions.unique.MadnessAction());
#每多一行 多随机一次
#actionManager.addToBottom(new com.megacrit.cardcrawl.actions.unique.MadnessAction());
#actionManager.addToBottom(new com.megacrit.cardcrawl.actions.unique.MadnessAction());
ENDRULE

RULE 能量不重置
CLASS com.megacrit.cardcrawl.ui.panels.EnergyPanel
METHOD setEnergy(int)
AT ENTRY
IF true
DO 
com.megacrit.cardcrawl.ui.panels.EnergyPanel.addEnergy($1);
return;
ENDRULE

RULE 遗物 干瘪之手 加强 任意牌都生效1
CLASS com.megacrit.cardcrawl.relics.MummifiedHand
METHOD onUseCard(com.megacrit.cardcrawl.cards.AbstractCard,com.megacrit.cardcrawl.actions.utility.UseCardAction)
AT ENTRY
BIND
card = $1;
IF true
DO 
link("MummifiedHand_cardType",card.type);
card.type=com.megacrit.cardcrawl.cards.AbstractCard$CardType.POWER;
ENDRULE

RULE 遗物 干瘪之手 加强 任意牌都生效2
CLASS com.megacrit.cardcrawl.relics.MummifiedHand
METHOD onUseCard(com.megacrit.cardcrawl.cards.AbstractCard,com.megacrit.cardcrawl.actions.utility.UseCardAction)
AT EXIT
BIND
card = $1;
ctype:com.megacrit.cardcrawl.cards.AbstractCard$CardType = linked("MummifiedHand_cardType");
IF true
DO 
card.type=ctype;
unlink("MummifiedHand_cardType");
ENDRULE

RULE 多倍金钱 5 倍
CLASS com.megacrit.cardcrawl.characters.AbstractPlayer
METHOD gainGold(int)
AT ENTRY
BIND
#倍数设置
mult=2;
IF true
DO 
$1=$1*mult;
ENDRULE

RULE 金钱 只加不减
CLASS com.megacrit.cardcrawl.characters.AbstractPlayer
METHOD loseGold(int)
AT ENTRY
IF 0#true
DO 
#$this.gainGold($1);
$1=0;
ENDRULE

RULE 商店无限删卡
CLASS com.megacrit.cardcrawl.shop.ShopScreen
METHOD updatePurge()
AT EXIT
BIND
shopScreen=com.megacrit.cardcrawl.dungeons.AbstractDungeon.shopScreen;
IF true
DO 
shopScreen.purgeAvailable = true;
ENDRULE

RULE 商店无限遗物
CLASS com.megacrit.cardcrawl.shop.ShopScreen
METHOD updateRelics()
AT EXIT
IF $this.relics.isEmpty()
DO 
$this.initRelics();
ENDRULE

RULE 商店无限卡牌
CLASS com.megacrit.cardcrawl.shop.ShopScreen
METHOD updateCards()
AT EXIT
BIND 
coloredCards = new java.util.ArrayList();
colorlessCards = new java.util.ArrayList();
IF $this.coloredCards.isEmpty() && $this.colorlessCards.isEmpty()
DO 
$this.coloredCards.add(com.megacrit.cardcrawl.dungeons.AbstractDungeon.getCardFromPool(com.megacrit.cardcrawl.dungeons.AbstractDungeon.rollRarity(), com.megacrit.cardcrawl.cards.AbstractCard$CardType.ATTACK, true).makeCopy());
$this.coloredCards.add(com.megacrit.cardcrawl.dungeons.AbstractDungeon.getCardFromPool(com.megacrit.cardcrawl.dungeons.AbstractDungeon.rollRarity(), com.megacrit.cardcrawl.cards.AbstractCard$CardType.ATTACK, true).makeCopy());
$this.coloredCards.add(com.megacrit.cardcrawl.dungeons.AbstractDungeon.getCardFromPool(com.megacrit.cardcrawl.dungeons.AbstractDungeon.rollRarity(), com.megacrit.cardcrawl.cards.AbstractCard$CardType.SKILL, true).makeCopy());
$this.coloredCards.add(com.megacrit.cardcrawl.dungeons.AbstractDungeon.getCardFromPool(com.megacrit.cardcrawl.dungeons.AbstractDungeon.rollRarity(), com.megacrit.cardcrawl.cards.AbstractCard$CardType.SKILL, true).makeCopy());
$this.coloredCards.add(com.megacrit.cardcrawl.dungeons.AbstractDungeon.getCardFromPool(com.megacrit.cardcrawl.dungeons.AbstractDungeon.rollRarity(), com.megacrit.cardcrawl.cards.AbstractCard$CardType.POWER, true).makeCopy());
$this.colorlessCards.add(com.megacrit.cardcrawl.dungeons.AbstractDungeon.getColorlessCardFromPool(com.megacrit.cardcrawl.cards.AbstractCard$CardRarity.UNCOMMON).makeCopy());
$this.colorlessCards.add(com.megacrit.cardcrawl.dungeons.AbstractDungeon.getColorlessCardFromPool(com.megacrit.cardcrawl.cards.AbstractCard$CardRarity.RARE).makeCopy());
$this.initCards();
ENDRULE

RULE 营火无限弃牌
CLASS com.megacrit.cardcrawl.vfx.campfire.CampfireTokeEffect
METHOD update()
AT EXIT
BIND ui:com.megacrit.cardcrawl.rooms.RestRoom = com.megacrit.cardcrawl.dungeons.AbstractDungeon.getCurrRoom();
IF TRUE
DO 
ui.campfireUI.somethingSelected = false;
ENDRULE

RULE 营火无限升级
CLASS com.megacrit.cardcrawl.vfx.campfire.CampfireSmithEffect
METHOD update()
AT EXIT
BIND ui:com.megacrit.cardcrawl.rooms.RestRoom = com.megacrit.cardcrawl.dungeons.AbstractDungeon.getCurrRoom();
IF TRUE
DO 
ui.campfireUI.somethingSelected = false;
ENDRULE

RULE 营火 休息 升级 不受 遗物影响1
CLASS com.megacrit.cardcrawl.rooms.CampfireUI
METHOD initializeButtons()
AT INVOKE com.megacrit.cardcrawl.characters.AbstractPlayer.hasRelic(String) ALL
BIND 
param = $@[1];
IF TRUE
DO 
link("CampfireUI_hasRelic",param);
ENDRULE

RULE 营火 休息 升级 不受 遗物影响2
CLASS com.megacrit.cardcrawl.rooms.CampfireUI
METHOD initializeButtons()
AFTER INVOKE com.megacrit.cardcrawl.characters.AbstractPlayer.hasRelic(String) ALL
BIND 
relicName:String = linked("CampfireUI_hasRelic");
IF relicName == "Coffee Dripper" || relicName == "Fusion Hammer"
DO 
unlink("CampfireUI_hasRelic");
$! = false;
ENDRULE

RULE 营火 休息 升级 不受 挑战影响1
CLASS com.megacrit.cardcrawl.rooms.CampfireUI
METHOD initializeButtons()
AT INVOKE java.util.HashMap.get(Object) ALL
BIND 
param = $@[1];
IF TRUE
DO 
link("CampfireUI_negativeMods",param);
ENDRULE

RULE 营火 休息 升级 不受 挑战影响2
CLASS com.megacrit.cardcrawl.rooms.CampfireUI
METHOD initializeButtons()
AFTER INVOKE java.util.HashMap.get(Object) ALL
BIND 
modName:String = linked("CampfireUI_negativeMods");
IF modName == "Midas"
DO 
unlink("CampfireUI_negativeMods");
$! = false;
ENDRULE

RULE 营火 功能过多 处理
CLASS com.megacrit.cardcrawl.rooms.CampfireUI
METHOD initializeButtons()
AT WRITE $cannotProceed
IF $this.buttons.size() == 5
DO 
abc.def.SimpleHelper.SetCampfireUIButtonPosition($this.buttons);
ENDRULE

RULE 关闭作弊检测1
CLASS com.megacrit.cardcrawl.dungeons.AbstractDungeon
METHOD isCheater()
BIND 
cheater=com.megacrit.cardcrawl.core.CardCrawlGame.cheater;
AT ENTRY
IF true
DO 
cheater = false;
return false;
ENDRULE

RULE 关闭作弊检测2
CLASS com.megacrit.cardcrawl.dungeons.AbstractDungeon
METHOD nextRoomTransition()
AT ENTRY
BIND 
cheater = com.megacrit.cardcrawl.core.CardCrawlGame.cheater;
IF true
DO 
cheater = false;
ENDRULE

RULE 关闭作弊检测3
CLASS com.megacrit.cardcrawl.screens.DeathScreen
METHOD antiCheatLogic()
AT ENTRY
BIND 
cheater = com.megacrit.cardcrawl.core.CardCrawlGame.cheater;
IF true
DO 
cheater = false;
return;
ENDRULE

RULE 总是获胜
CLASS com.megacrit.cardcrawl.screens.DeathScreen
METHOD <init>(com.megacrit.cardcrawl.monsters.MonsterGroup)
AFTER WRITE isVictory ALL
IF $this.isVictory==false
DO 
$this.isVictory=true;
ENDRULE

RULE 总是升级
CLASS com.megacrit.cardcrawl.screens.DeathScreen
METHOD calculateUnlockProgress()
AFTER WRITE score ALL
IF 0#$this.score < 10000
DO 
$this.score=10000;
ENDRULE

RULE 移除多余的头环
CLASS com.megacrit.cardcrawl.relics.AbstractRelic
METHOD instantObtain(com.megacrit.cardcrawl.characters.AbstractPlayer,int,boolean)
AT ENTRY
BIND
player = $1;
IF TRUE
DO 
abc.def.SimpleHelper.RemoveCirclet(player);
ENDRULE

RULE 移除多余的头环 加载时Fix
CLASS com.megacrit.cardcrawl.core.CardCrawlGame
METHOD loadPlayerSave(com.megacrit.cardcrawl.characters.AbstractPlayer)
AT INVOKE com.megacrit.cardcrawl.relics.AbstractRelic.instantObtain(com.megacrit.cardcrawl.characters.AbstractPlayer,int,boolean) ALL
BIND
player = $1;
saveFile = com.megacrit.cardcrawl.core.CardCrawlGame.saveFile;
IF ($s=="Circlet" || $s=="Red Circlet") && $index < saveFile.relic_counters.size()
DO 
saveFile.relic_counters.remove($index);
ENDRULE